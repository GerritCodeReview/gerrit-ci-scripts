pipeline {
        agent { label 'aws' }

        parameters {
            string(name: 'AWS_PREFIX', defaultValue:"jenkins", description: 'A string to prefix stacks and resources with')
            string(name: 'AWS_REGION', defaultValue:"us-east-1", description: 'Which region to deploy to')

            string(name: 'HOSTED_ZONE_NAME', description: 'Name of the hosted zone')
            string(name: 'CLUSTER_INSTANCE_TYPE', defaultValue: 'm4.xlarge', description:'The EC2 instance Type used to run the cluster')

            string(name: 'DOCKER_REGISTRY_URI', description: 'URI of the Docker registry')
            string(name: 'SSL_CERTIFICATE_ARN', description: 'ARN of the wildcard SSL Certificate')

            string(name: 'LDAP_SERVER', description:'URL of the organizationâ€™s LDAP server to query for user information and group membership from')
            string(name: 'LDAP_USERNAME', description: 'Username to bind to the LDAP server with')
            string(name: 'LDAP_ACCOUNT_BASE', description: 'Root of the tree containing all user accounts')
            string(name: 'LDAP_GROUP_BASE', description: 'Root of the tree containing all group objects')

            string(name: 'SMTP_SERVER', description: 'Hostname (or IP address) of a SMTP server that will relay messages generated by Gerrit to end users')
            string(name: 'SMTP_USER', description: 'User name to authenticate with')
            string(name: 'SMTP_DOMAIN', description: 'Domain to be used in the "From" field of any generated email messages')

            string(name: 'METRICS_CLOUDWATCH_NAMESPACE', defaultValue: 'jenkins', description: 'The CloudWatch namespace for Gerrit metrics')
            string(name: 'SUBDOMAIN', defaultValue: '$(AWS_PREFIX)-master-demo', description: 'Name of the master sub domain')
            string(name: 'GERRIT_KEY_PREFIX', defaultValue: 'gerrit_secret', description: 'Secrets prefix')
            string(name: 'GERRIT_DOCKER_IMAGE_TAG', description: 'Gerrit master docker image')

            string(name: 'GERRIT_HTTP_URL', description: 'Gerrit GUI URL')
            string(name: 'GERRIT_SSH_URL', description: 'Gerrit SSH URL')
            string(name: 'GIT_HTTP_USERNAME', description: 'Username for Git/HTTP testing')
            string(name: 'GIT_HTTP_PASSWORD', description: 'Password for Git/HTTP testing')
            string(name: 'GERRIT_PROJECT', defaultValue: 'load-test', description: 'Gerrit project for load test')
            string(name: 'ACCOUNT_COOKIE', description: 'HTTP Cookie to access the Gerrit GUI')
            string(name: 'XSRF_TOKEN', description: 'XSRF_TOKEN Cookie to access the Gerrit GUI for pOST operations')
            string(name: 'NUM_USERS', defaultValue: '10', description: 'Number of concurrent user sessions')
            string(name: 'DURATION', defaultValue: '2 minutes', description: 'Total duration of the test')
        }
        stages{
            stage("Setup single-master aws stack") {
                steps {
                    checkout scm
                     withCredentials(
                        [[
                            $class: 'AmazonWebServicesCredentialsBinding',
                            accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                            credentialsId: 'aws-credentials-id',  // ID of credentials in Jenkins
                            secretKeyVariable: 'AWS_SECRET_ACCESS_KEY'
                        ]]) {
                        sh "export AWS_ACCESS_KEY_ID=${AWS_ACCESS_KEY_ID}"
                        sh "export AWS_SECRET_ACCESS_KEY=${AWS_SECRET_ACCESS_KEY}"
                         dir ('single-master') {
                            script {
                                def setupData = readFile(file:"setup.env.template")
                                setupData = resolveParameter(setupData, "HOSTED_ZONE_NAME", HOSTED_ZONE_NAME)
                                setupData = resolveParameter(setupData, "CLUSTER_INSTANCE_TYPE", CLUSTER_INSTANCE_TYPE)
                                setupData = resolveParameter(setupData, "DOCKER_REGISTRY_URI", DOCKER_REGISTRY_URI)
                                setupData = resolveParameter(setupData, "SSL_CERTIFICATE_ARN", SSL_CERTIFICATE_ARN)

                                setupData = resolveParameter(setupData, "LDAP_SERVER", LDAP_SERVER)
                                setupData = resolveParameter(setupData, "LDAP_USERNAME", LDAP_USERNAME)
                                setupData = resolveParameter(setupData, "LDAP_ACCOUNT_BASE", LDAP_ACCOUNT_BASE)
                                setupData = resolveParameter(setupData, "LDAP_GROUP_BASE", LDAP_GROUP_BASE)

                                setupData = resolveParameter(setupData, "SMTP_SERVER", SMTP_SERVER)
                                setupData = resolveParameter(setupData, "SMTP_USER", SMTP_USER)
                                setupData = resolveParameter(setupData, "SMTP_DOMAIN", SMTP_DOMAIN)
                                setupData = resolveParameter(setupData, "METRICS_CLOUDWATCH_NAMESPACE",METRICS_CLOUDWATCH_NAMESPACE)
                                setupData = resolveParameter(setupData, 'SUBDOMAIN', SUBDOMAIN)

                                setupData = setupData + "\nGERRIT_KEY_PREFIX:= ${GERRIT_KEY_PREFIX}"

                                writeFile(file:"setup.env", text: setupData)
                            }
                            sh "make IMAGE_TAG=${GERRIT_DOCKER_IMAGE_TAG} AWS_REGION=${AWS_REGION} AWS_PREFIX=${AWS_PREFIX} create-all"
                         }
                     }
                }
            }
            stage('Pull newest Gatling tests docker image') {
                steps {
                    sh 'docker pull gerritforge/gatling-sbt-gerrit-test'
                }
            }

            stage('Run Gatling tests') {
                steps {
                    script {
                        writeFile(file:"simulation.env", text: """
                                GERRIT_HTTP_URL="${GERRIT_HTTP_URL}"
                                GERRIT_SSH_URL="${GERRIT_SSH_URL}"
                                ACCOUNT_COOKIE="${ACCOUNT_COOKIE}"
                                GIT_HTTP_USERNAME="${GIT_HTTP_USERNAME}"
                                GIT_HTTP_PASSWORD="${GIT_HTTP_PASSWORD}"
                                XSRF_TOKEN="${XSRF_TOKEN}"
                                GERRIT_PROJECT="${GERRIT_PROJECT}"
                                GIT_SSH_COMMAND="ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no"
                           """)
                    }
                    script {
                        for (simulation in ["GerritGitSimulation", "GerritRestSimulation"]) {
                            sh """\
                                docker run --rm --env-file simulation.env -v `pwd`/target/gatling:/opt/gatling/results \
                                gerritforge/gatling-sbt-gerrit-test -s gerritforge.${simulation}
                               """
                        }
                    }

                    gatlingArchive()
                }
            }

        }
        post {
            cleanup {
                withCredentials(
                        [[
                            $class: 'AmazonWebServicesCredentialsBinding',
                            accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                            credentialsId: 'aws-credentials-id',  // ID of credentials in Jenkins
                            secretKeyVariable: 'AWS_SECRET_ACCESS_KEY'
                        ]]) {
                    sh "export AWS_ACCESS_KEY_ID=${AWS_ACCESS_KEY_ID}"
                    sh "export AWS_SECRET_ACCESS_KEY=${AWS_SECRET_ACCESS_KEY}"
                    dir ('single-master') {
                        sh "make AWS_REGION=${AWS_REGION} AWS_PREFIX=${AWS_PREFIX} delete-all"
                    }
                }
            }
        }
}

def resolveParameter(String text, String paramName, String paramValue) {
    return text.split('\n').collect { l ->
        def targetLine = l.trim().startsWith(paramName)
        targetLine ? "${paramName}:=${paramValue}" : l
    }.join('\n')
}